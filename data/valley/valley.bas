  99 REM ** DEFINE MAJOR VARIABLES
 100 DIM D(3), G(73), P(8), N(8), S(4), T(2)
 110 DIM M$(18),MS(18),N1(18)
 120 VG$="":GC$="":F$="":DL$=""
 130 TS=0:TN=0:TM=3:CF=0
 140 D$="{HOM}{21 CD}"
 150 D1$=LEFT$(D$,17)
 160 SP$="{39 SPC}"
 170 R$="{30 CR}"
 180 R1$=LEFT$(R$,21)

 299 REM ** SKIP SCENE DATA
 300 FOR I=1 TO 32
 310 READ C$
 320 NEXT I

 329 REM ** LOAD MONSTER DATA
 330 FOR I=0 TO 18
 340 READ M$(I):READ MS(I):READ N1(I)
 350 NEXT I

 999 REM ** CHARACTER CHOICE AND LOAD
1000 PRINT "{CLS}{CD}LOAD A CHARACTER FROM TAPE (Y/N) ?"
1010 VG$="YN":GOSUB 1500:REM ** UNIGET
1020 INPUT "{CD}CHARACTER'S NAME {2 CR}*{3 CL}";J$
1030 IF J$="*" THEN 1020
1040 IF LEN(J$)>16 THEN PRINT "{CD}TOO LONG":GOTO 1020
1050 IF GC$="N" THEN 1240
1060 PRINT "{CLS}PLACE DATA TAPE IN THE TAPE DECK"
1070 PRINT "{CD}IS IT REWOUND ?"
1080 GOSUB 1600:REM ** ANYKEY
1090 OPEN 1,1,0,J$
1100 INPUT#1,P$
1110 INPUT#1,TS
1120 INPUT#1,EX
1130 INPUT#1,TN
1140 INPUT#1,CS
1150 INPUT#1,PS
1160 INPUT#1,PT(0)
1170 INPUT#1,PT(1)
1180 INPUT#1,PT(2)
1190 INPUT#1,C1
1200 INPUT#1,P1
1210 CLOSE 1
1220 C=150
1230 GOTO 14000
1240 PRINT "{CLS}{2 CD}CHARACTER TYPES...CHOOSE CAREFULLY"
1250 PRINT
1260 PRINT "WIZARD    (1)"
1270 PRINT "THINKER   (2)"
1280 PRINT "BARBARIAN (3)","KEY 1-5"
1290 PRINT "WARRIOR   (4)"
1300 PRINT "CLERIC    (5)"
1310 GET GC$:IF GC$="" THEN 1310
1320 A=VAL(GC$)
1330 IF A=1 THEN P$="WIZARD":P1=2:C1=0.5:CS=22:PS=28
1340 IF A=2 THEN P$="THINKER":P1=1.5:C1=0.75:CS=24:PS=26
1350 IF A=3 THEN P$="BARBARIAN":P1=0.5:C1=2:CS=28:PS=22
1360 IF A=4 THEN P$="WARRIOR":P1=1:C1=1.25:CS=26:PS=24
1370 IF A=5 THEN P$="CLERIC":P1=1.25:C1=1:CS=25:PS=25
1380 IF A<1 OR A>5 THEN P$="DOLT":P1=1:C1=1:CS=20:PS=20
1390 EX=5:C=150
1400 PRINT "{2 CD}GOOD LUCK"
1410 PRINT "{CD}";J$;" THE ";P$
1420 DF=150:DL$="D":GOSUB 36000:REM ** DELAY
1430 GOSUB 10000:REM ** VALLEY DRAW
1440 DF=5:GOSUB 36000:REM ** DELAY + UPDATE
1450 GOTO 2000:REM ** MOVEMENT

1499 REM ** UNIGET ROUTINE
1500 GET GC$:IF GC$="" THEN 1500
1510 FOR I=1 TO LEN(VG$)
1520 IF MID$(VG$,I,1)=GC$ THEN RETURN
1530 NEXT I
1540 GOTO 1500

1599 REM ** ANYKEY ROUTINE
1600 PRINT "{CD}** PRESS ANY KEY TO CONTINUE **"
1610 GET GC$:IF GC$="" THEN 1610
1620 RETURN

1699 REM ** COMBAT GET ROUTINE
1700 FOR I=1 TO 10:GET GC$:NEXT I:REM ** EMPTIES BUFFER
1710 TV=0
1720 FOR I=1 TO 60
1730 GET GC$:IF GC$="" THEN 1750
1740 GOTO 1770
1750 NEXT I
1760 TV=1:REM ** NO KEY PRESSED
1770 PRINT D$;SP$:REM ** WIPE AWAY MESSAGE
1780 RETURN

1999 REM ** MOVEMENT ROUTINE
2000 M=W:PK=PEEK(W):POKE M,81
2010 C=C+10
2020 IF PK=77 OR PK=78 THEN 2040
2030 PRINT D$;"YOUR MOVE...WHICH DIRECTION ?":GOTO 2050
2040 PRINT D$;"SAFE ON THE PATH...WHICH WAY ?"
2050 FOR I=1 TO 10:GET GC$:NEXT I:REM ** CLEAR KBD BUFFER
2060 GET GC$:IF GC$="E" THEN 45000:REM ** EGO

2069 REM ** SPECIAL ROUTINE FOR NUMERIC KEYPADS
2070 A=VAL(GC$):IF A=0 THEN 2060
2090 W=M+A-2-40*(INT((VAL(GC$)-1)/3)-1)
2100 TN=TN+1:PRINT D$;SP$

2109 REM ** AM I STEPPING ON SOMETHING
2110 Q=81:Q1=PEEK(W):IF Q1=32 OR Q1=45 THEN 2190
2120 IF Q1=219 THEN 48000:REM ** QUIT
2130 IF Q1=214 OR Q1=160 OR Q1=80 THEN TN=TN-1:GOTO 2030:REM ** HIT WALL OR TREE
2140 IF Q1=216 OR Q1=87 OR Q1=173 OR Q1=230 THEN 9000:REM ** SCENE ENTRY
2150 IF Q1=104 OR Q1=96 THEN 9090:REM ** SCENE EXIT
2160 IF Q1=102 THEN 15000:REM *** STAIRS
2170 IF Q1=224 OR (GC$="5" AND PK=224) THEN Q=209:C=C-20:IF C<=0 THEN 5500:REM ** WATER
2180 IF Q1=42 THEN 2800:REM ** SPECIAL FIND
2190 POKE M,PK:PK=PEEK(W):M=W:POKE M,Q
2200 IF PK=77 OR PK=78 THEN DF=5:GOTO 2250

2209 REM ** NOTHING, MONSTER OR FIND PERHAPS
2210 RF=RND(TI)
2220 IF RF<0.33 THEN 3000:REM ** MONSTER SELECT
2230 IF RF<0.75 THEN 2300:REM ** FIND SELECT
2240 PRINT D$;"NOTHING OF VALUE...SEARCH ON":DF=80
2250 GOSUB 36000:REM ** DELAY + UPDATE
2260 GOTO 2010

2299 REM ** FINDS ROUTINE
2300 RF=INT(RND(TI)*6+1)
2310 ON RF GOSUB 2340,2380,2380,2410,2410,2440
2320 DF=80:GOSUB 36000:REM ** DELAY + UPDATE
2330 GOTO 2010
2340 PRINT D$;"A CIRCLE OF EVIL...DEPART IN HASTE !"
2350 CS=CS+INT((FL+1)/2):PS=PS-INT((FL+1)/2):C=C-20
2360 IF C<=0 THEN 55000:REM ** DEATH
2370 RETURN
2380 PRINT D$;"A HOARD OF GOLD"
2390 TS=TS+INT(FL*RND(TI)*100*100)
2400 RETURN
2410 PRINT D$;"YOU FEEL THE AURA OF THE DEEP MAGIC..."
2420 PRINT "{8 SPC}...ALL AROUND YOU..."
2430 GOTO 2450
2440 PRINT D$;"...A PLACE OF ANCIENT POWER..."
2450 PS=PS+2+INT(FL*P1):CS=CS+1+INT(FL*C1):C=C+25
2460 RETURN

2799 REM ** SPECIAL FINDS ROUTINE
2800 POKE M,32:M=W:PK=32:POKE M,81
2810 RN=RND(TI):PRINT D$;SP$
2820 IF S=6 AND RN>0.95 AND T(1)=6 AND T(2)=0 AND RT>25 THEN T(2)=1:GOTO 2870
2830 IF S=5 AND RN>0.7 AND T(0)=0 THEN T(0)=1:GOTO 2880
2840 IF S=4 AND RN>0.7 AND T(0)=1 AND T(1)<6 AND FL>T(1) THEN 2890
2850 IF RN>0.43 THEN PRINT D$;"A WORTHLESS BAUBLE":GOTO 2940
2860 PRINT D$;"A PRECIOUS STONE !":GOTO 2930
2870 PRINT D$;"YOU FIND THE HELM OF EVANNA !":GOTO 2930
2880 PRINT D$;"THE AMULET OF ALARIAN...EMPTY...":GOTO 2930
2885 REM NOTE: ORIGINAL CODE HAD PRINT STATEMENT AFTER 2890
2890 PRINT D$;"AN AMULET STONE..."
2900 DF=60:DL$="D":GOSUB 36000:REM ** DELAY
2910 IF RN>0.85 THEN PRINT "{CD}...BUT THE WRONG ONE !":GOTO 2940
2920 PRINT "{CD}...THE STONE FITS !":T(1)=T(1)+1
2930 TS=TS+100*(T(0)+T(1)+T(2)+FL)
2940 DF=80:GOSUB 36000:REM ** DELAY + UPDATE
2950 GOTO 2010

2999 REM ** MONSTER SELECTION ROUTINE
3000 PRINT D$;"** BEWARE...THOU HAST ENCOUNTERED **"
3010 MS=0:N=0:CF=1
3020 RF=INT(RND(TI)*17):IF RF>9 AND RND(TI)>0.85 THEN 3020
3030 IF Q1=224 OR PK=224 THEN RF=INT(RND(TI)*2+17)
3040 IF RF=16 AND RND(TI)<0.7 THEN 3020
3050 IF FL<5 AND RF=15 THEN 3020
3060 X$=LEFT$(M$(RF),1)
3070 FOR I=1 TO LEN(F$)
3080 IF MID$(F$,I,1)=X$ THEN 3110
3090 NEXT I
3100 GOTO 3020
3110 M$=RIGHT$(M$(RF),LEN(M$(RF))-1)
3120 IF MS(RF)=0 THEN 3150
3130 MS=INT((CS*0.3)+MS(RF)*FL^0.2/(RND(TI)+1))
3140 IF N1(RF)=0 THEN 3160
3150 N=INT(N1(RF)*FL^0.2/(RND(TI)+1))
3160 U=INT((RF+1)*(FL^1.5))
3170 IF RF>23 THEN U=INT((RF-22)*FL^1.5)
3180 PRINT "{CD}";LEFT$(R$,12-(LEN(M$))/2);"AN EVIL ";M$
3190 DF=40:GOSUB 3600:REM ** DELAY + UPDATE

3499 REM ** CHARACTER'S COMBAT ROUTINE
3500 IF RND(TI)<0.6 THEN 4000:REM ** MONSTER'S COMBAT
3510 PRINT D$;"YOU HAVE SURPRISE...ATTACK OR RETREAT"
3520 GOSUB 1700:REM ** COMBAT GET
3530 IF GC$="R" THEN 3900
3540 IF TV=1 THEN 3600
3550 IF GC$<>"A" THEN 4000
3560 DF=30:DL$="D":GOSUB 36000:REM ** DELAY
3570 PRINT D$;"*** STRIKE QUICKLY***"
3580 GOSUB 1700:REM ** COMBAT GET
3590 IF TV=0 THEN 3620
3600 PRINT D$;"* TOO SLOW...TOO SLOW *"
3610 HF=0:GOTO 3830
3620 E=39*LOG(EX)/3.14
3630 IF GC$="S" THEN 4500:REM ** SPELL CONTROL
3640 IF MS=0 THEN PRINT D$;"YOUR SWORD AVAILS YOU NOUGHT HERE":GOTO 3830
3650 C=C-1
3660 IF C<=0 THEN PRINT D$;"YOU FATALLY EXHAUST YOURSELF":GOTO 55000:REM ** DEATH
3670 RF=RND(TI)*10
3680 IF GC$="H" AND (RF<5 OR CS>NS*4) THEN Z=2:GOTO 3730
3690 IF GC$="B" AND (RF<7 OR CS>NS*4) THEN Z=1:GOTO 3730
3700 IF GC$="L" AND (RF<9 OR CS>NS*4) THEN Z=0.3:GOTO 3730
3710 PRINT D$;"YOU MISSED IT !"
3720 HF=0:GOTO 3830
3730 IF HF=1 THEN D=MS+INT(RND(TI)*9):HF=0:GOTO 3760
3740 D=INT((((CS*50*RND(TI))-(10*MS)+E)/100)*Z):IF D<0 THEN D=0
3750 IF CS>(MD-D)*4 THEN HF=1
3760 MS=MS-D
3770 PRINT D$;"A HIT..."
3780 DF=60:DL$="D":GOSUB 36000:REM ** DELAY
3790 IF D=0 THEN PRINT D$;"{8 CR}BUT...NO DAMAGE":HF=0:GOTO 3830
3800 PRINT D$;"{8 CR}";D;" DAMAGE...":IF MS<=0 THEN 3860:REM ** IT'S DEAD
3810 IF HF=1 THEN DF=30:DL$="D":GOSUB 36000:REM ** DELAY
3820 IF HF=1 THEN PRINT "{CD}THE ";M$;" STAGGERS DEFEATED"
3830 DF=110:GOSUB 36000:REM ** DELAY + UPDATE
3840 IF HF=1 THEN 3570
3850 GOTO 4000:REM ** MONSTER'S COMBAT
3860 PRINT D$;"{2 CD}...KILLING THE MONSTER..."
3870 EX=EX+U:HF=0:CF=0
3880 DF=80:GOSUB 36000:REM ** DELAY + UPDATE
3890 GOTO 2010:REM ** MOVEMENT
3900 PRINT D$;"KNAVISH COWARD !":CF=0
3910 GOTO 3880

3999 REM ** MONSTER'S COMBAT ROUTINE
4000 PRINT D$;"THE CREATURE ATTACKS..."
4010 DF=50:DL$="W":GOSUB 36000:REM *** DELAY + WIPE
4020 IF MS=0 THEN 4300:REM ** PSIONIC ATTACK
4030 IF MS<N AND N>6 AND RND(TI)<0.5 THEN 4300
4040 MS=MS-1:IF MS<=0 THEN 4240
4050 RF=INT(RND(TI)*10+1)
4060 ON RF GOTO 4070,4080,4090,4100,4110,4110,4120,4120,4130,4140
4070 PRINT D$;"IT SWINGS AT YOU AND MISSES":GOTO 4280
4080 PRINT D$;"YOUR BLADE DEFLECTS THE BLOW":GOTO 4280
4090 PRINT D$;"...BUT HESITATES, UNSURE...":GOTO 4280
4100 Z=3:PRINT D$;"IT STRIKES YOUR HEAD !":GOTO 4150
4110 Z=1.5:PRINT D$;"YOUR CHEST IS STRUCK !":GOTO 4150
4120 Z=1:PRINT D$;"A STRIKE AT YOUR SWORDARM !":GOTO 4150
4130 Z=1.3:PRINT D$;"A BLOW TO YOUR BODY !":GOTO 4150
4140 Z=0.5:PRINT D$;"IT CATCHES YOUR LEGS !"
4150 DF=60:DL$="D":GOSUB 36000:REM ** DELAY
4160 G=INT((((MS*75*RND(TI))-(10*CS)-E)/100)*Z)
4170 IF G<0 THEN G=0:PRINT D$;"... SAVED BY YOUR ARMOUR !{2 SPC}":GOTO 4280
4180 C=C-G
4190 IF G>9 THEN CS=INT(CS-G/6)
4200 IF G=0 THEN PRINT D$;"SHAKEN......BUT NO DAMAGE DONE":GOTO 4280
4210 PRINT D$;"YOU TAKE...{6 SPC}{6 CL}";G;" DAMAGE...{6 SPC}"
4220 IF CS<=0 OR C<=0 THEN 55000:REM ** DEATH
4230 GOTO 4280
4240 PRINT D$;"...USING ITS LAST ENERGY IN THE ATTEMPT"
4250 EX=INT(EX+U/2):CF=0
4260 DF=100:GOSUB 36000:REM ** DELAY + UPDATE
4270 GOTO 2010:REM ** MOVEMENT
4280 DF=100:GOSUB 36000:REM ** DELAY + UPDATE
4290 GOTO 3570:REM ** CHARACTER'S COMBAT

4299 REM ** MONSTER'S PSIONIC ATTACK
4300 PRINT D$;"...HURLING A LIGHTNING BLOT AT YOU !"
4310 G=INT(((180*NRND(TI))-(PS+E))/100):N=N-5:IF G>9 THEN N=N=INT(G/5)
4320 DF=80:DL$="W":GOSUB 36000:REM ** DELAY + WIPE
4330 IF N<=0 THEN N=0:GOTO 4240
4340 IF RND(TI)<0.25 THEN 4410
4350 IF G<=0 THEN G=0:GOTO 4400
4360 PRINT D$;"IT STRIKES HOME !"
4370 DF=110:GOSUB 36000:REM ** DELAY + UPDATE
4380 C=C-G:IF G>9 THEN PS=INT(PS-G/4)
4390 GOTO 4210
4400 PRINT D$;"YOUR PSI SHIELD PROTECTS YOU":GOTO 4280
4410 PRINT D$;"...MISSED YOU !":GOTO 4280

4499 REM ** SPELL CONTROL ROUTINE
4500 PRINT D$;"WHICH SPELL SEEK YE ? ":OSUB 1700:REM ** COMBAT GET
4510 IF TV=1 THEN 3600:REM ** TOO SLOW
4520 IF VAL(GC$)>0 AND VAL(GC$)=<3 THEN 4540
4530 PRINT D$;"NO DUCH SPELL...{5 SPC}":GOTO 4640
4540 IF 4*PS*RND(TI)<=N THEN 4590
4550 ON VAL(GC$) GOSUB 5000,5200,5400

4559 REM ** SC CONTAINS OUTCOME FLAG
4560 ON SC GOTO 4620,4640,4660,4570,4600,4580,4590
4570 PRINT D$;"IF IS BEYOND YOU{5 SPC}":GOTO 4640
4580 PRINT "BUT THE SPELL FAILS...!":GOTO 4640
4590 PRINT D$;"NO USE, THE BEAST'S PSI SHEILDS IT":GOTO 4640
4600 PRINT D$;"THE SPELL SAPS ALL YOUR STRENGTH"
4610 GOTO 55000:REM ** DEATH
4620 DF=100:GOSUB 36000:REM ** DELAY + UPDATE
4630 GOTO 2010:REM ** MOVEMENT
4640 DF=60:GOSUB 36000:REM ** DELAY + UPDATE
4650 GOTO 4000:REM ** MONSTER'S COMBAT
4660 DF=60:GOSUB 36000:REM ** DELAY + UPDATE
4670 GOTO 3570:REM ** CHARACTER'S COMBAT

4999 REM ** SPELL 1 (SLEEPIT)
5000 C=C-5:IF C<=0 THEN SC=5:RETURN
5010 PRINT D$;"SLEEP YOU FOUL FIEND THAT I MAY ESCAPE"
5020 PRINT "AND PRESERVE MY MISERABLE SKIN"
5030 DF=180:GOSUB 36000:REM ** DELAY + UPDATE
5040 PRINT D$;"THE CREATURE STAGGERS..."
5050 DF=40:GOSUB 36000:REM ** DELAY + UPDATE
5060 IF RND(TI)<0.5 THEN 5090
5070 PRINT "AND COLLAPSES...STUNNED"
5080 EX=INT(EX+U/2):CF=0:SC=1:RETURN
5090 PRINT "BUT RECOVERS WITH A SNARL !"
5100 SC=2:RETURN

5199 REM ** SPELL 2 (PSI-LANCE)
5200 IF MS>C OR PS<49 OR EX<1000 THEN SC=4:RETURN
5210 C=C-10:IF C<=0 THEN SC=5:RETURN
5220 IF N=0 THE PRINT D$;"THIS BEAST HAS NO PSI TO ATTACK":SC=2:RETURN
5230 PRINT D$;"WITH MY MIND I BATTLE THEE FOR MY LIKE"
5240 DF=120:GOSUB 36000:REM ** DELAY + UPDATE
5250 RF=RND(TI):IF RF<0.4 AND N>10 THEN SC=6:RETURN
5260 D=INT((((CD*50*RF)-5*(MS+N)+E)/50)/4)
5270 IF D<=0 THEN D=0:SC=7:RETURN
5280 PRINT D$;"THE PSI-LANCE CAUSES ";D*2;" DAMAGE"
5290 N=N-3*D:IF N<=0 THEN N=0
5300 MS=MD-D:IF MS<=0 THEN MS=0
5310 IF (MS+N)>0 THEN SC=2:RETURN
5320 PRINT "{CD}...KILLING THE CREATURE"
5330 EX=EX+U:CF=0:SC=1:RETURN

5399 REM ** SPELL 3 (CRISPIT)
5400 IF PS<77 OR EX<5000 THEN SC=4:RETURN
5410 C=C-20:IF C<=0 THE SC=4:RETURN
5420 PRINT D$;"WITH THE MIGHT OF MY SWORD I SMITE THEE"
5430 PRINT "WITH THE POWER OF MY SPELL I CURSE THEE"
5440 PRINT "BURN YE SPAWN OF NELL AND SUFFER..."
5450 DF=240:GOSUB 36000:REM ** DELAY + UPDATE
5460 PRINT D$;"A BOLT OF ENERGY LASHES AT THE BEAST..."
5470 DF=80DL$="W":GOSUB 36000:REM ** DELAY + UPDATE + WIPE
5480 IF RND(TI)>(PS/780)*(5-PI) THEN PRINT D$;"MISSED IT !":SC=2:RETURN
5490 D=INT((CS+PS+RND(TI))-(10*N*RND(TI)))
5500 IF D<=0 THEN D=0:SC=7:RETURN
5510 IF MS=0 THEN N=N-D:GOTO 5530
5520 MS=MS-D:IF D>10 THEN N=INT(N-(D/3))
5530 PRINT D$;"IT STRIKES HOME CAUSED ";D;" DAMAGE {2 SPC}!"
5540 IF (MS+N)<=0 THEN 5570
5550 DF=80:DL$="D":GOSUB 36000:REM ** DELAY
5560 SC=2:RETURN
5570 PRINT "{CD}THE BEAST DOES SCREAMING !"
5580 EX=EX+U:CF=0:SC=1:RETURN

8999 REM ** SCENARIO CONTROL ROUTINE
9000 IF Q1=230 AND PK=224 THEN PRINT D$;"YOU CANNOT ENTER THIS WAY...":GOTO 9110
9010 FOR I=2 TO 7
9020 P(I)=0
9030 N(I)=INT(RND(TI)*5+4)
9040 IF N(I)=5 THEN 9030
9050 NEXT I
9060 IF S=1 THEN MP=M
9070 P(2)=INT(RND(T1)*30+1)
9080 TF=TN:GOTO 9130

9089 REM ** EXIT FROM SCENARIO
9090 IF TN>TF+INT(RND(TI)*6+1) THEN 9130
9100 PRINT D$;"THE WAY IS BARRED"
9110 TN=TN-1:C=C-10:DF-100:DL$="W":GOSUB 36000:REM ** DELAY + UPDATE + WIPE
9120 GOTO 2010
9130 C=C-10:POKE M,32:POKE W,Q
9140 IF Q1=96 THEN S=1:FL=1
9150 IF Q1=104 AND S=4 THEN S=1:FL=1
9160 IF Q1=104 AND S=5 OR S=6 THEN S=S-3:FL=FL-4:M=MW
9170 IF Q1=173 THEN S=2:FL=2
9180 IF Q1=216 THEN S=3:FL=3
9190 IF Q1=216 OR Q1=173 THEN D2$=LEFT$(D$,INT(RND(TI)*10)):R2$=LEFT$(R$,P(2))
9200 IF Q1=87 THEN S=4:FL=2
9210 IF Q1=230 THEN S=S+3:FL=FL+4:MW=M
9220 ON S GOSUB 10000,12000,12010,14000,14010,14010
9230 DF=5:GOSUB 36000:REM ** DELAY + UPDATE
9240 GOTO 2000:REM ** MOVEMENT

 9999 REM ** SCENARIO 1 (THE VALLEY)
10000 PRINT "{CLS}":F$="VAEGH":FL=1:S=1

10009 REM ** DRAW THE VALLEY FRAME
10010 PRINT "{HOM}{REV}{39^V}{OFF}"
10020 FOR I=1 TO 12
10030 PRINT "{REV}{^V}{OFF}{37 SPC}{REV}{^V}{OFF}"
10040 NEXT I
10050 PRINT "{REV}{39^V}{OFF}"

10059 REM ** IF PATH ALREADY DRAWN SKIP
10060 IF G(0)<>0 THEN 10190

10069 REM ** COMPUTE THE PATH
10070 M=32809+(INT(RND(TI)*11+1)*40)
10080 L=M:MP=M:W=M:G(0)=M:G(1)=219
10090 FOR I=2 TO 72 STEP 2
10100 IF RND(TI)>0.5 THEN 10120
10110 PC=77:L1=L+41:GOTO 10130
10120 PC=78:L1=L-39
10130 IF L1>=33286 OR L1<=32806 THEN 10100
10140 G(I+1)=PC
10150 IF I>2 AND G(I+1)<>G(I-1) THEN L1=L+1
10160 G(I)=L1:L=L1:POKE G(I),G(I+1)
10170 NEXT I
10180 G(73)=219
10190 FOR I=0 TO 72 STEP 2
10200 POKE G(I),G(I+1)
10210 NEXT I
10220 IF S(0)<>0 THEN 10280
10230 FOR I=0 TO 4
10240 N1=INT(RND(TI)*11)+1:N2=INT(RND(TI)*34)+1
10250 S(I)=32809+(40*N1)+N2
10260 IF PEEK(S(I))<>32 OR PEEK(S(I)+1)<>32 THEN 10240
10270 NEXT I

10279 REM ** PLOT IN SCENARIOS
10280 POKE S(0),216:POKE S(0)+1,216:POKE S(1),216:POKE S(1)+1,216
10290 POKE S(2),173:POKE S(2)+1,173:POKE S(3),173:POKE S(3)+1,173
10300 POKE S(4),87
10310 M=MP:W=M
10320 RETURN

11999 REM ** SCENARIO 2 (WOODS AND SWAMPS)
12000 F$="AFL":PC=45:GOTO 12020
12010 F$="FAEHL":PC=88
12020 PK=32
12030 PRINT "{CLS}"

12039 REM ** DRAW RANDOM WOODS OR SWAMPS
12040 L=32810
12050 FOR I=1 TO 200
12060 POKE L+INT(RND(TI)*515),PC
12070 NEXT I

12079 REM ** PRINT IN LAKE
12090 PRINT "{HOM}";D2$;R2$;"{2 CR}{REV}{2 SPC}{OFF}"
12100 PRINT R2$;"{REV}{2 SPC}{OFF}{2 SPC}{REV}{2 SPC}{OFF}"
12110 PRINT R2$;"{REV}{2 SPC}{^&}{OFF}{SPC}{REV}{3 SPC}{OFF}"
12120 PRINT R2$;"{CR}{REV}{4 SPC}{OFF}{CR}{REV}{2 SPC}{OFF}"
12130 PRINT R2$;"{3 CR}{REV}{2 SPC}{OFF}"
12140 PRINT R2$;"{4 CR}{REV}{SPC}{OFF}"

12149 REM ** DRAW IN THE FRAME
12150 PRINT "{HOM}{40^SPC}";
12160 FOR I=1 TO 13
12170 PRINT "{^SPC}{38 CR}{^SPC}";
12180 NEXT I
12190 PRINT "{40^SPC}"
12200 POKE 33306,32:W=33306
12210 IF Q1=104 THEN M=MW:W=M
12220 RETURN

13999 REM ** SCENARIO 3 (CASTLE-TYPES)
14000 F$="CAGE":P=0:H=N(FL):PK=32:GOTO 14020
14010 F$="CBE":P=0:H=N(FL):PK=32:P(FL)=P(2)

14019 REM ** DRAW FRAME
14020 PRINT "{CLS}{REV}{2 CR}{21 SPC}{OFF}"
14030 FOR I=1 TO 13
14040 PRINT "{REV}{2 CR}{SPC}{OFF}{19 SPC}{REV}{SPC}{OFF}"
14050 NEXT I
14060 PRINT "{REV}{2 CR}{21 SPC}{OFF}"

14069 REM *** DRAW VERTICAL WALL
14070 RESTORE:FOR I=1 TO P(FL)
14080 READ V:IF V=100 THEN RESTORE
14090 NEXT I
14100 L1=32810
14110 FOR J=1 TO 3
14120 READ D(J):P=P+1
14130 IF D(J)=100 THEN RESTORE:D(J)=3:P=P+1
14140 NEXT J
14150 FOR I=0 TO H:PC=160
14160 L=L1+(40*I):IF L>33290 THEN 14260
14170 IF I=1 THEN PC=32
14180 IF D(1)=0 THEN PC=160:GOTO 14200
14190 POKE L+D(1),PC:PC=160
14200 IF I=3 THEN PC=32
14210 POKE L+D(1)+D(2),PC=160
14220 IF I=4 THEN PC=32
14230 POKE L+D(1)+D(2)+D(3),PC:PC=160
14240 NEXT I
14250 L1=L1+(4)*H)+40:GOTO 14110
14259 REM ** DRAW HORIZONTAL WALLS
14260 L1=32810
14270 FOR J=1 TO 4
14280 L=L1+(40*J*(H+1))
14290 FOR K=1 TO 19
14300 IF L>33250 THEN 14350
14310 POKE L+K,PC
14320 IF K=2 OR K=3*H OR K=17 THEN POKE L+K,32:POKE L+K-40,32:POKE L+K+40,32
14330 NEXT K
14340 NEXT J
14350 IF S=5 OR S=6 THEN 14380
14360 IF FL/2=INT(FL/2) THEN POKE 33291,102:GOTO 14380
14370 POKE 32829,102

14379 REM ** DOORWAY NEEDED ?
14380 IF FL=2 OR S=5 OR S=6 THEN POKE 33336,104:POKE 33296,32
14390 IF P(3)=0 THEN W=33296
14400 IF S=5 THEN 14470
14410 IF S=6 THEN 14450
14420 PRINT "{HOM}";R1$;"{4 CD}{3 CR}THE BLACK TOWER"
14430 PRINT R1$;"{3 CR}{3 SPC}OF ZAEXON"
14440 PRINT R1$;"{3 CD}{3 CR}{3 SPC}FLOOR ";FL-1:GOTO 14490
14450 PRINT "{HOM}";R1$;"{2 CD}{5 CR}{REV}{SPC}VOUNUM'S{SPC}{OFF}"
14460 PRINT R1$;"{5 CR}{REV}{3 SPC}LAIR{3 SPC}{OFF}":GOTO 14500
14470 PRINT "{HOM}";R1$;"{2 CD}{4 CR}{REV}THE TEMPLE OF{OFF}"
14480 PRINT R1$;"{4 CR}{REV}{2 SPC}Y'NAGIOTH{2 SPC}{OFF}"
14490 P(FL+1)=P(FL)+P

14499 REM ** SCATTER SPECIAL FINDS
14500 IF FL<4 OR RND(TI)<0.3 THEN RETURN
14510 FOR I=1 TO INT(RND(TI)*5)+2
14520 N1=INT(RND(TI)*19)
14530 N2=INT(RND(TI)*12)
14540 IF PEEK(32811+40*N2+N1)<>32 THEN 14520
14550 POKE (32811+40*N2+N1),42
14560 NEXT I
14570 RETURN

14999 REM ** STAIRS ROUTINE
15000 POKE W,*1:POKE M,32
15010 PRINT D$;"A STARIRWAY...UP OR DOWN ?":TV=FL
15020 VG$="UD":GOSUB 1500:REM ** UNIGET
15030 IF GC$="U" THEN FL=FL+1:GOTO 15050
15040 FL=FL-1
15050 IF FL>7 OR FL<2 THEN 15080
15060 DF=110:DL$="D":GOSUB 36000:REM ** DELAY
15070 GOTO 9220
15080 PRINT D$;"THESE STAIRS ARE BLOCKED{SPC}"
15090 DF=60:DL$="D":GOSUB 36000:REM ** DELAY
15100 FL=TV:GOTO 15010

35999 REM ** DELAY, WIPE & UPDATE ROUTINE
36000 REM FOR DL=1 TO (DF*TM)
36010 REM NEXT DL
36015 DELAY DF*TM
36020 IF DL$="D" THEN DL$="":RETURN
36030 PRINT D$;SP$
36040 PRINT SP$
36050 PRINT SP$
36060 IF DL$="W" THEN DL$="":RETURN
36070 IF CS>77-INT(2*P1^2.5) THEN CS=77-INT(2*P1^2.5)
36080 IF PS<7 THEN PS=7
36090 IF PS>INT(42*(P1+1)^LOG(P1^3.7))+75 THEN PS=INT(42*(P1+1)^LOG(P1^3.7))+75
36100 IF C>125-(INT(P1)*12.5) THEN C=125-INT(INT(P1)*12.5)
36110 PRINT D1$;"{CU}";J$,P$
36120 PRINT "TREASURE   =";TS
36130 PRINT "EXPERIENCE =";EX
36140 PRINT "TURNS      =";TN
36150 PRINT D1$;R1$;"COMBAT STR ={4 SPC}{4 CL}";CS
36160 PRINT R1$;"PSI POWER  ={4 SPC}{4 CL}";PS
36170 PRINT R1$;"STAMINA    ={4 SPC}{4 CL}";C

36179 REM ** IF FIGHTING UPDATE MONSTER
36180 IF CF=1 THEN 36210
36190 PRINT SP$
36200 RETURN
36210 PRINT D$;"{2 CU}{REV}";M$;"{OFF}";
36220 PRINT D$;R1$;"{2 CU}M STR ={12 SPC}{12 CL}";MS;N;"{4 SPC}"
36230 RETURN

44999 REM ** RATING ROUTINE
45000 DF=5:DL$="W":GOSUB 36000:REM ** DELAY + WIPE
45010 RT=INT(0.067*(EX+TS/3)^0.5+LOG(EX/((TN+1)^1.5))):IF RT>28 THEN RT=28
45020 IF RT<0 THEN RT=0
45030 PRINT D$;"YOUR RATING NOW BE";RT
45040 IF T(2)=1 THEN PRINT "YOU HAVE THE HELM OF EVANNA"
45050 IF T(0)=1 THEN PRINT "AMULET SOMES...{SPC}";T(1)
45060 DF=250:DL$="W":GOSUB 36000:REM ** DELAY + WIPE
45070 IF GC$="E" THEN C=C-10:GC$="":GOTO 2010:REM ** MOVEMENT
45080 RETURN

47999 REM ** QUIT VALLEY ROUTINE
48000 PRINT D$;"THOU ART SAFE IN A CASTLE":IF CS<20 THEN CS=20
48010 POKE M,PK:PK=PEEK(W):M=W:POKE M,Q
48020 PRINT "WILT THOU LEAVE THE VALLEY (Y/N) ?"
48030 VG$="YN":GOSUB 1500:REM ** UNIGET
48040 DF=5:DL$="W":GOSUB 36000:REM ** DELAY + WIPE
48050 GOSUB 45000:REM ** RATING
48060 DF=110:DL$="W":GOSUB 36000:REM ** DELAY + WIPE
48070 IF GC$="Y" THEN 50000:REM ** SAVE ROUTINE
48080 C=150:PRINT D$;"THY WOUNDS HEALED...THY SWORD SHARP"
48090 PRINT "GO AS THE GODS DEMAND...TRUST NONE OTHER"
48100 DF=120:GOSUB 36000:REM ** DELAY + UPDATE
48110 GOTO 2010:REM ** MOVEMENT

49999 REM ** SAVE CHARACTER ROUTINE
50000 PRINT "{CLS}DO YOU WISH TO SAVE ";J$;" ?"
50010 PRINT:PRINT "PLEASE KEY Y OR N"
50020 VG$="YN":GOSUB 1500:REM ** UNIGET
50030 IF GC$="N" THEN 50210
50040 PRINT "{CLS}PLACE YOUR CASSETTE IN THE TAPE DECK"
50050 PRINT "IS IT REWOUND ?"
50060 GOSUB 1600:REM ** ANYKEY

50069 REM ** THIS IS FOR PET ONLY
50070 OPEN 1,1,1,J$
50080 PRINT#1,P$
50090 PRINT#1,T$
50100 PRINT#1,EX
50110 PRINT#1,TN
50120 PRINT#1,CS
50130 PRINT#1,PS
50140 PRINT#1,T(0)
50150 PRINT#1,T(1)
50160 PRINT#1,T(2)
50170 PRINT#1,C1
50180 PRINT#1,P1
50190 CLOSE 1
50200 PRINT "{CLS}{3 CD}"," *** DONE ***"
50210 PRINT D$;"{6 SPC}TYPE RUN TO START AGAIN"
50220 CLR
50230 END

54999 REM ** DEATH ROUTINE
55000 C=0:CS=0:PS=0:CF=0
55010 DF=100:GOSUB 36000:REM ** DELAY + UPDATE
55020 IF T(1)=6 THEN 55070
55030 PRINT D$,"{CR}OH WHAT A FRAIL SHELL"
55040 PRINT D$,"{2 CR}IS THIS THAT WE CALL MAN"
55050 DF=300:DL$="W":GOSUB 36000:REM ** DELAY + WIPE
55060 PRINT "{CLS}":GOTO 50210
55069 REM ** RESTORE CHARACTER TO LIFE
55070 T(0)=0:T(1)=0:TS=0:CS=30:C=150:PS=30
55080 PRINT D$;"ALARIAN'S AMULET PROTECTS THY SOUL"
55090 PRINT "{CD}{REV}{2 SPC}LIVE AGAIN{2 SPC}{OFF}"
55100 DF=150:GOSUB 36000:REM ** DELAY + UPDATE
55110 L=G(0):MP=L:M=W:S=1:GOTO 9220:REM ** SCENE CONTROL

59999 REM ** DATA FOR CASTLE TYPE SCENARIOS
60000 DATA 4,7,3,6,4,4,6,5,3,6,0,3,8,4,3,5,5,3,8,3,4,5,0,6,3,6,4,6,4,7,4,100

60009 REM ** DATA FOR MONSTERS
60010 DATA AWOLFEN,9,0,AHOB-GOBLIN,9,0,AORC,9,0,EFIRE-IMP,7,3,GROCK-TROLL,19,0
60020 DATA EHARPY,10,12,AOGRE,23,0,BBARROW-WIGHT,0,25,HCENTAUR,18,14
60030 DATA EFIRE-GIANT,26,20,VTHUNDER-LIZARD,50,0,CMINOTAUR,35,25,CWRAITH,0,30
60040 DATA FWYVERN,36,12,BDRAGON,50,20,CRING-WRAITH,0,45,ABALROG,50,50

60049 REM ** SPECIAL MONSTERS FOR WATER ONLY
60050 DATA LWATER-IMP,15,15,LKRAKEN,50,0
